program             declaration* EOF ;
declaration         varDecl | statement ;
varDecl             "var" IDENTIFIER ( "=" expression )? ";" ;
statement           exprStmt | printStmt | block | ifStmt | whileStmt ;
ifStmt              "if" "(" expression ")" statement ("else" statement)? ;
exprStmt            expression ";" ;
printStmt           "print" expression ";" ;
block               "{" declaration* "}" ;
whileStmt           "while" "(" expression ")" statement ;
expression          asignment ;
asignment           IDENTIFIER "=" assignment | logic_or ;
logic_or            logic_and ( "or" logic_and )* ;
logic_and           equality ( "and" equality )* ;
equality            comparison ( ( "!=" | "==" ) comparison )* ;
comparison          term ( ( "<" | "<=" | ">" | ">=" ) term )* ;
term                factor ( ("+" | "-" ) factor )* ;
factor              unary ( ( "*" | "/" ) unary )* ;
unary               ( "-" | "!" ) unary | primary ;
primary             NUMBER | STRING | "true" | "false" | "nil" | "(" expression ")" | IDENTIFIER ;